plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'ir.ninjacoder.codesnap'
    compileSdk 35

    defaultConfig {
        minSdk 23
        targetSdk 35
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        viewBinding true
    }

    lint {
        checkReleaseBuilds = false
        abortOnError = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

/* ────────────────
   Dependencies
────────────────── */
dependencies {
    implementation("com.google.android.material:material:1.14.0-alpha06")
    runtimeOnly 'androidx.graphics:graphics-shapes:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'org.antlr:antlr4:4.13.2'
    implementation 'com.google.code.gson:gson:2.13.1'

    implementation("io.noties.markwon:html:4.6.2")
    implementation("io.noties.markwon:image:4.6.2")
    implementation("io.noties.markwon:ext-tasklist:4.6.2")
    implementation("io.noties.markwon:image-glide:4.6.2")
    implementation("io.noties.markwon:core:4.6.2")
    implementation("io.noties.markwon:syntax-highlight:4.6.2") {
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "org.jetbrains", module: "annotations-java5"
    }

    implementation("com.github.bumptech.glide:glide:5.0.5")
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                // خروجی AAR
                artifact(bundleReleaseAar)

                groupId = 'com.github.HanzoDev1375'
                artifactId = 'CodeSnap'
                version = VERSION_NAME

                pom {
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        configurations.implementation.allDependencies.each { dep ->
                            if (dep.group && dep.name && dep.version) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', dep.group)
                                dependencyNode.appendNode('artifactId', dep.name)
                                dependencyNode.appendNode('version', dep.version)
                            }
                        }
                    }
                }
            }
        }
    }
}